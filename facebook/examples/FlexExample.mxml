<?xml version="1.0" encoding="utf-8"?>
<s:WindowedApplication xmlns:fx="http://ns.adobe.com/mxml/2009" 
					   xmlns:s="library://ns.adobe.com/flex/spark" 
					   xmlns:mx="library://ns.adobe.com/flex/mx" minWidth="800" minHeight="600" applicationComplete="init(event)">
	<fx:Script>
		<![CDATA[
			import mx.events.FlexEvent;
			
			private static const CLIENT_ID:String = "";
			private static const CLIENT_SECRET:String = "";

			private var webView:StageWebView;
			private var code:String;
			private var access_token:String;
			private var accessTokenLoader:URLLoader;
			private var profileLoader:URLLoader;
			
			protected function init(event:FlexEvent):void
			{
				webView = new StageWebView(true);
				webView.addEventListener(LocationChangeEvent.LOCATION_CHANGE, changeLocation);
				webView.stage = this.stage;
				webView.viewPort = new Rectangle(0, 0, stage.stageWidth, stage.stageHeight);
				webView.loadURL("https://www.facebook.com/dialog/oauth?client_id="+CLIENT_ID+"&redirect_uri=https://www.facebook.com/connect/login_success.html&scope=email");
			}
			
			private function changeLocation(event:LocationChangeEvent):void
			{
				var location:String = webView.location;
						
				if(location.indexOf("code=") != -1){
					webView.dispose();
					code = location.substr(location.indexOf("code=")+5, location.length);
					getAccessToken();
				}
								
			}
			
			private function getAccessToken():void
			{				
				var request:URLRequest = new URLRequest("https://graph.facebook.com/v2.3/oauth/access_token?client_id="+CLIENT_ID+"&redirect_uri=https://www.facebook.com/connect/login_success.html&client_secret="+CLIENT_SECRET+"&code="+code);
				
				accessTokenLoader = new URLLoader();
				accessTokenLoader.addEventListener(Event.COMPLETE, accessTokenReceived);
				accessTokenLoader.load(request);
			}
			
			private function accessTokenReceived(event:Event):void
			{
				var rawData:Object = JSON.parse(String(event.currentTarget.data));
				access_token = rawData.access_token;
				
				loadProfileInfo();
			}
			
			private function loadProfileInfo():void
			{
				profileLoader = new URLLoader();
				profileLoader.dataFormat = URLLoaderDataFormat.TEXT;
				profileLoader.addEventListener(Event.COMPLETE, profileLoaded);
				profileLoader.load(new URLRequest("https://graph.facebook.com/me/?access_token="+access_token+"&fields=name,email,picture.type(large)"));
			}
			
			private function profileLoaded(event:Event):void
			{
				trace(event.currentTarget.data);
			}
			
		]]>
	</fx:Script>
	
</s:WindowedApplication>
